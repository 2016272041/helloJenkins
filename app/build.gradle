apply plugin: 'com.android.application'

buildscript {

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.2'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
    }
}

//Version plugin task -> dependencyUpdates
apply plugin: 'com.github.ben-manes.versions'
//Method count
apply plugin: 'com.getkeepsafe.dexcount'
//Code Coverage
apply plugin: 'jacoco-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.greenlionsoft.hellojenkins"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-DEBUG"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->

            def jenkinsJobName = System.getenv('JOB_NAME')
            def jenkinsJobBuild = System.getenv('BUILD_NUMBER')

            if (jenkinsJobName) {
                //Jenkins build
                def date = new Date();
                def formattedDate = date.format('yyyyMMdd_HHmm')

                output.outputFile = new File(output.outputFile.parent,
                        output.outputFile.name.replace(".apk",
                                "_Hello_Jenkins_JN"
                                        + jenkinsJobName + "_JB"
                                        + jenkinsJobBuild + "_"
                                        + formattedDate + "_VC"
                                        + defaultConfig.versionCode + "_"
                                        + defaultConfig.versionName + "_"
                                        + ".apk"))

            } else {
                //Android Studio or local build
                output.outputFile = new File(output.outputFile.parent,
                        output.outputFile.name.replace(".apk", "_Hello_Jenkins_VC" + defaultConfig.versionCode + "_" + defaultConfig.versionName + "_" + ".apk"))
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.1.0'
    testCompile 'junit:junit:4.12'
}

//Version plugin task -> dependencyUpdates
//configures Gradle Versions plugin to list only release status versions
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

sonarqube {
    properties {
        property "sonar.jacoco.reportPath", "build/jacoco/testDebugUnitTest.exec"
        property "sonar.junit.reportsPath", "build/test-results/debug"
    }
}

jacoco {
    toolVersion = jacocoVersion
}
